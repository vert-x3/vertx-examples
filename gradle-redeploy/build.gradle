plugins {
  id 'java'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
  jcenter()
  maven {
    url "https://oss.sonatype.org/content/repositories/iovertx-3744/"
  }
}

version = '3.5.1'
sourceCompatibility = '1.8'

dependencies {
  compile "io.vertx:vertx-core:$version"
}

mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'io.vertx.example.HelloWorldVerticle'

// Vert.x watches for file changes in all subdirectories
// of src/ but only for files with .java extension
def watchForChange = 'src/**/*.java'

// Vert.x will call this task on changes
def doOnChange
if (System.getProperty("os.name").toLowerCase().contains("windows")) {
  doOnChange = '.\\gradlew classes'
} else {
  doOnChange = './gradlew classes'
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

shadowJar {
  classifier = 'fat'

  manifest {
      attributes "Main-Verticle": mainVerticleName
  }

  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.10'
}
